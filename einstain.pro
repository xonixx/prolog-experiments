
:- use_module(library(clpfd)).

задача_эйнштейна(Решение) :-
	Национальности = [Англичанин, Швед, Датчанин, Норвежец, Немец],
	Цвета = [Красный, Зеленый, Белый, Желтый, Голубой],
	Животные = [Собака, Птица, Кошка, Лошадь, _Рыба],
	Напитки = [Чай, Кофе, Молоко, Пиво, Вода],
	Сигареты = [Pallmall, Dunhill, Marlboro, Winfield, Rothmans],
	
	Решение = [
		Национальности,
		Цвета,
		Животные,
		Напитки,
		Сигареты
	       ],
	append(Решение, ВсеПараметры),
	
	ВсеПараметры ins 1..5,
	
	all_different(Национальности),
	all_different(Цвета),
	all_different(Животные),
	all_different(Напитки),
	all_different(Сигареты),
	
	Англичанин #= Красный,
	Швед #= Собака,
	Датчанин #= Чай,
	Белый - Зеленый #= 1,
	Зеленый #= Кофе,
	Pallmall #= Птица,
	Молоко #= 3,
	Желтый #= Dunhill,
	Норвежец #= 1,
	abs(Marlboro - Кошка) #= 1,
	abs(Лошадь - Dunhill) #= 1,
	Winfield #= Пиво,
	abs(Норвежец - Голубой) #= 1,
	Немец #= Rothmans,
	abs(Marlboro - Вода) #= 1,
	
	labeling([],ВсеПараметры).
